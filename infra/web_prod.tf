resource "aws_s3_bucket" "prod_web" {
  bucket = "new.seslogin.com"
}

resource "aws_s3_bucket_public_access_block" "prod_web" {
  bucket                  = aws_s3_bucket.prod_web.id
  block_public_acls       = true
  ignore_public_acls      = true
  block_public_policy     = true
  restrict_public_buckets = true
}

resource "aws_cloudfront_origin_access_control" "prod_web" {
  name                              = "seslogin-prod-web"
  origin_access_control_origin_type = "s3"
  signing_behavior                  = "always"
  signing_protocol                  = "sigv4"
}

resource "aws_s3_bucket_policy" "prod_web" {
  bucket = aws_s3_bucket.prod_web.id
  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [{
      Sid       = "AllowCloudFrontOAC"
      Effect    = "Allow"
      Principal = { Service = "cloudfront.amazonaws.com" }
      Action    = "s3:GetObject"
      Resource  = "arn:aws:s3:::new.seslogin.com/*"
      Condition = { StringEquals = {
        "AWS:SourceArn" = aws_cloudfront_distribution.prod.arn
      } }
    }]
  })
}

data "aws_cloudfront_function" "request_rewrite" {
  name  = "seslogin-request-rewrite"
  stage = "LIVE"
}

resource "aws_cloudfront_distribution" "prod" {
  aliases             = ["seslogin.com", "new.seslogin.com"]
  enabled             = true
  http_version        = "http2"
  is_ipv6_enabled     = true
  price_class         = "PriceClass_All"
  default_root_object = "index.html"

  origin {
    origin_id                = "prod-web-s3"
    domain_name              = aws_s3_bucket.prod_web.bucket_regional_domain_name
    origin_access_control_id = aws_cloudfront_origin_access_control.prod_web.id
  }

  default_cache_behavior {
    target_origin_id       = "prod-web-s3"
    viewer_protocol_policy = "redirect-to-https"
    allowed_methods        = ["GET", "HEAD"]
    cached_methods         = ["GET", "HEAD"]
    compress               = true
    min_ttl                = 0
    default_ttl            = 300
    max_ttl                = 300
    forwarded_values {
      query_string = false
      cookies { forward = "none" }
    }
  }

  ordered_cache_behavior {
    path_pattern           = "/assets/*"
    target_origin_id       = "prod-web-s3"
    viewer_protocol_policy = "redirect-to-https"
    allowed_methods        = ["GET", "HEAD"]
    cached_methods         = ["GET", "HEAD"]
    compress               = true
    cache_policy_id        = "658327ea-f89d-4fab-a63d-7e88639e58f6"
    function_association {
      event_type   = "viewer-request"
      function_arn = data.aws_cloudfront_function.request_rewrite.arn
    }
  }

  # OAC returns 403 (not 404) for missing S3 keys — catch both for SPA routing
  custom_error_response {
    error_code            = 403
    response_code         = 200
    response_page_path    = "/index.html"
    error_caching_min_ttl = 10
  }
  custom_error_response {
    error_code            = 404
    response_code         = 200
    response_page_path    = "/index.html"
    error_caching_min_ttl = 10
  }

  restrictions {
    geo_restriction { restriction_type = "none" }
  }

  viewer_certificate {
    acm_certificate_arn      = aws_acm_certificate.prod.arn
    ssl_support_method       = "sni-only"
    minimum_protocol_version = "TLSv1.2_2021"
  }
}
